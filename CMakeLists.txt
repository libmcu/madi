# SPDX-License-Identifier: Apache-2.0

cmake_minimum_required(VERSION 3.20.0)

if (CMAKE_VERSION VERSION_GREATER_EQUAL "3.24.0")
	cmake_policy(SET CMP0135 NEW)
endif()

set(BASEDIR ${CMAKE_CURRENT_LIST_DIR})
set(OUTDIR ${BASEDIR}/build)

if (DEFINED ESP_PLATFORM)
	if (NOT DEFINED BOARD)
		if (DEFINED IDF_TARGET)
			set(BOARD ${IDF_TARGET})
		else()
			set(BOARD esp32)
		endif()
	endif()
	set(CMAKE_TOOLCHAIN_FILE $ENV{IDF_PATH}/tools/cmake/toolchain-${BOARD}.cmake)
elseif (DEFINED WEST_PYTHON)
	set(OVERLAY_CONFIG ports/zephyr/fpl.conf)
	find_package(Zephyr REQUIRED HINTS $ENV{ZEPHYR_BASE})
endif()

project(${BOARD})

if (DEFINED ESP_PLATFORM)
	set(TARGET_PLATFORM esp32)
	set(PLATFORM_CMAKE_FILE ports/esp-idf/esp32.cmake)
elseif (DEFINED ZEPHYR_BASE)
	set(TARGET_PLATFORM zephyr)
	set(PLATFORM_CMAKE_FILE ports/zephyr/zephyr.cmake)
elseif (DEFINED BOARD)
	enable_language(C CXX ASM)
	set(PLATFORM_CMAKE_FILE projects/boards/${BOARD}.cmake)
	set(PREREQUISITE_ONLY true)
	include(${PLATFORM_CMAKE_FILE})
	unset(PREREQUISITE_ONLY)
else()
	message(FATAL_ERROR "Either of unknown platform or undefined board")
endif()

include(${BASEDIR}/projects/version.cmake)
include(${BASEDIR}/projects/app.cmake)
include(${PLATFORM_CMAKE_FILE})
